
data{

for(nv in nrnd){
tau.rho[nv]<-1
for(k in 1:nsp){
spX.rho[k,nv]<-0
}}

for(nv in nrnd2){
tau.psi[nv]<-1
for(k in 1:nsp){
spX.psi[k,nv]<-0
}}

}

model{

for(i in 1:nz){

y[i]~dbin(z[i]*rho[i],J[i])
z[i]~dbern(psi[i]*w[species[i]])

logit(rho[i])<-detl[i]
logit(psi[i])<-ocl[i]

detl[i]<- sum(vcoefs[i,])
ocl[i]<- sum(vcoefs2[i,])

for(v in 1:npred){
vcoefs[i,v]<-(rho0[v] + spX.rho [species[i],v]) * pred[i,v]
}

for(v in 1:npred2){
vcoefs2[i,v]<-(psi0[v] + spX.psi [species[i],v]) * pred2[i,v]
}

}


# Detection
# Fixed effects
for(v in 1:npred){
rho0[v] ~ dnorm(0,1/3^2)
}
# Random effects
for(v in rnd){
tau.rho[v] ~ dgamma(0.01,0.01)

for(k in 1:nsp){
spX.rho[k,v]~dnorm(0,1/tau.rho[v]^2)

}}

# Occupancy
# Fixed effects
for(v in 1:npred2){
psi0[v] ~ dnorm(0,1/3^2)
}
# Random effects
for(v in rnd2){
tau.psi[v] ~ dgamma(0.01,0.01)

for(k in 1:nsp){
spX.psi[k,v]~dnorm(0,1/tau.psi[v]^2)
}}

for(s in 1:nsp){
w[s]~dbern(omega[s])
omega[s]~dunif(0,1)
}

}
